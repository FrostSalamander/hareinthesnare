float totalSecrets = 5;
float totalSecretsFound = 0;

float jacobsEvidenceFound = 0;

void foundSecret()
{
    totalSecretsFound++;

    string msg = "You have found " + totalSecretsFound + " of " + totalSecrets + " secrets.";
    $secret_popup.setKey("text", msg);
    $secret_popup.setKey("force", 1);

    $secret_popup.activate($player1);
    sys.trigger($speaker_secret);   
}

void frob_jacobs_letter()
{
    $player1.addInvItem($letter_jacobs);
    sys.trigger($secret_jacobs_note);
}

void poison_stew(entity stew)
{
    // Only allow this if you've completed the first objective
    // and have found out what happened to Jacbos
    float objectiveState = $player1.getObjectiveState(1);

    if (objectiveState == 1 && jacobsEvidenceFound)
    {
        // remove poison from inventory
        entity curr_item = $player1.getCurInvItemEntity();
        $player1.changeInvItemCount(curr_item.getKey("inv_name"), curr_item.getKey("inv_category"), -1);

        // done, prevent further frobbing
        stew.setFrobable(0);
        stew.startSound("snd_addpoison", SND_CHANNEL_UNUSED, false);
        sys.wait(2);

        // objective complete
        $player1.setObjectiveState(4, 1);
    }
}

void markEvidenceFound()
{
    jacobsEvidenceFound = 1;
}

void triggerEnd()
{
    float objectiveWilliams = $player1.getObjectiveState(1); // Objective 1, get into Williams' footlocker
    float objectiveJacobs = $player1.getObjectiveState(2);   // Objective 2, find out what happened to Jacobs

    if ((objectiveWilliams == 1) && (objectiveJacobs == 1))
    {   
        $player1.setOrigin($debrief_rug.getOrigin());   // teleport to 'theatre room' rug on the floor
        sys.trigger($debrief_video);                    // start the debrief
        sys.trigger($debrief_speaker);                  // debrief audio
        sys.wait(57);                                   // video length in seconds
        $player1.setObjectiveState(8, 1);               // complete 'player exit' objective
    }
}

void main()
{
	sys.waitFrame();
}